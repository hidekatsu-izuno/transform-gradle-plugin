plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'net.arnx'
version = '0.1.0'

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

gradlePlugin {
    // Define the plugin
    plugins {
        fileconv {
            id = 'net.arnx.fileconv'
            implementationClass = 'net.arnx.fileconv.gradle.FileconvGradlePlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

task sourceJar(type : Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type : Jar, dependsOn : javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        sample(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier = 'sources'
                extension  = 'jar'
            }
            artifact javadocJar {
                classifier = 'javadoc'
                extension  = 'jar'
            }
        }
    }
    repositories {
        maven {
            url 'file:../maven_repo'
        }
    }
}
